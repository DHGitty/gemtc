model {
	for (i in 1:ns) {
		# Likelihood for each arm
		for (k in 1:na[i]) {
			@if{dichotomous}r[i, k] ~ dbin(p[i, k], n[i, k])
			logit(p[i, k]) <- mu[i] + delta[i, k]@else{}m[i, k] ~ dnorm(theta[i, k], prec[i, k])
			theta[i, k] <- mu[i] + delta[i, k]
			prec[i, k] <- pow(e[i, k], -2)@end{}
		}

		# Study-level relative effects
		w[i, 1] <- 0
		delta[i, 1] <- 0
		for (k in 2:na[i]) { # parameterize multi-arm trials using a trick to avoid dmnorm
			delta[i, k] ~ dnorm(md[i, k], taud[i, k])
			md[i, k] <- d[t[i, 1], t[i, k]] + sw[i, k]
			taud[i, k] <- tau.d * 2 * (k - 1) / k
			w[i, k] <- delta[i, k] - d[t[i, 1], t[i, k]]
			sw[i, k] <- sum(w[i, 1:k-1]) / (k - 1)
		}
	}

	# Relative effect matrix
@{relativeEffectMatrix}

	# Study baseline priors
	for (i in 1:ns) {
		mu[i] ~ dnorm(0, @{priorPrecision})
	}

	# Variance prior
	sd.d ~ dunif(0, @{stdDevUpperLimit})
	tau.d <- pow(sd.d, -2)@if{inconsistency}
	sd.w ~ dunif(0, @{stdDevUpperLimit})
	tau.w <- pow(sd.w, -2)@end{}

	# Effect parameter priors
	@foreach{p : parameters}@code{
		prec = p.getClass().equals(inconsClass) ? "tau.w" : priorPrecision
	}@{p} ~ dnorm(0, @{prec})@end{'\n\t'}
}
